plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }

    rawStrippedMods
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

def modsToStrip = [
        "addadd": "maven.modrinth:addadd:${project.additionaladditions_neoforge_version}",
        "deeperdarker": "maven.modrinth:deeperdarker:${project.deeperdarker_neoforge_version}",
        "enderite-mod": "maven.modrinth:enderite-mod:${project.enderitemod_neoforge_version}"
]

dependencies {
    modsToStrip.values().each { coord ->
        rawStrippedMods(coord)
    }
}

def strippedDir = file("$buildDir/stripped-mods")
if (!strippedDir.exists()) {
    strippedDir.mkdirs()
}

def resolvedJars = configurations.rawStrippedMods.resolvedConfiguration.resolvedArtifacts

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"
    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    resolvedJars.each { artifact ->
        def modKey = modsToStrip.find { it.value.contains(artifact.name) }?.key ?: artifact.name
        def strippedFile = file("$strippedDir/${modKey}-stripped-${artifact.moduleVersion.id.version}.jar")
        
        if (!strippedFile.exists()) {
            ant.zip(destfile: strippedFile) {
                zipfileset(src: artifact.file, excludes: "architectury.common.json")
            }
        }
        modImplementation(files(strippedFile))
    }

    // Barched
    modImplementation("maven.modrinth:barched:${project.barched_neoforge_version}")

    // Config
    modApi("me.shedaniel.cloth:cloth-config-neoforge:$rootProject.cloth_config_version")

    // Compat Mods
    modImplementation("maven.modrinth:bronze:${project.bronze_neoforge_version}")
//    modImplementation("maven.modrinth:paradise-lost:${project.paradise_lost_neoforge_version}") //unsupported
//    modImplementation("maven.modrinth:progression-reborn:${project.progression_reborn_neoforge_version}") //unsupported
//    modImplementation("maven.modrinth:dragonloot:${project.dragonloot_neoforge_version}") //unsupported
    modImplementation("maven.modrinth:aether:${project.aether_neoforge_version}")

    // Utility Mods
    modImplementation("maven.modrinth:jei:${project.jei_neoforge_version}")
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}